buildscript {
  ext {
    springBootVersion = '2.3.3.RELEASE'
  }
  repositories {
    jcenter()
    maven { url "https://repo.spring.io/milestone" }
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

allprojects {
  group = 'io.confluent.developer'
  version = '0.0.1-SNAPSHOT'
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'maven-publish'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  repositories {
    jcenter()
  }

  sourceCompatibility = '11'

  publishing {
    publications {
      maven(MavenPublication) {
        from(components.java)
      }
    }
  }

  tasks.withType(JavaCompile) {
    options.compilerArgs = [
        '-Xlint:deprecation',
        '-Werror'
    ]
  }

  tasks.withType(Test) {
    testLogging {
      outputs.upToDateWhen { false }
      events 'standard_out'
      exceptionFormat = "full"
    }
  }
}
